use types::*;

use std::str::FromStr;

grammar;

pub Expr: Expression = {
	<a:Expr> "+" <b:Factor> => Expression::Add(Box::new(a), Box::new(b)),
	<a:Expr> "-" <b:Factor> => Expression::Subtract(Box::new(a), Box::new(b)),
	Factor,
};

Factor: Expression = {
	<a:Factor> "*" <b:Term> => Expression::Multiply(Box::new(a), Box::new(b)),
	<a:Factor> "/" <b:Term> => Expression::Divide(Box::new(a), Box::new(b)),
	Term
};

Term: Expression = {
	Int => Expression::Const(Value::Int(<>)),
	Float => Expression::Const(Value::Float(<>)),
	Boolean => Expression::Const(Value::Boolean(<>)),
	<a:Term> <b:InvMultTerm> => Expression::Multiply(Box::new(a), Box::new(b)),
	InvMultTerm
};

InvMultTerm: Expression = {
	"(" <a:Expr> ")" => a,
	Variable => Expression::Variable(<>)
};

Int: i32 = r"[0-9]+" => i32::from_str(<>).unwrap();
Float: f64 = r"[0-9]+\.[0-9]+" => f64::from_str(<>).unwrap();
Boolean: bool = {
	"true" => true,
	"false" => false
};

Variable: String = r"[A-Za-z]+" => <>.to_owned();
